plugins {
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'maven'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.9.0'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

scmVersion {
    tag {
        prefix = ''
    }
}

group 'com.github.mkopylec'
archivesBaseName = name
version scmVersion.version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.0.1.RELEASE'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.2.RELEASE'
    compile group: 'com.ryantenney.metrics', name: 'metrics-spring', version: '3.1.3'
    compile group: 'io.dropwizard.metrics', name: 'metrics-graphite', version: '3.2.6'

    compileOnly group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: '2.0.1.RELEASE'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.1.RELEASE'
    testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.16.0'
}

configurations {
    all*.exclude group: 'org.mortbay.jetty', module: 'servlet-api'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    if (project.ext.has('signArtifacts')) {
        sign configurations.archives
    }
}

ext {
    ossrhUsername = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
    ossrhPassword = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'Charon Spring Boot Starter'
                packaging 'jar'
                description 'Reverse proxy implementation in form of Spring Boot starter.'
                url 'https://github.com/mkopylec/charon-spring-boot-starter'
                scm {
                    connection 'scm:git:https://github.com/mkopylec/charon-spring-boot-starter.git'
                    developerConnection 'scm:git:https://github.com/mkopylec/charon-spring-boot-starter.git'
                    url 'https://github.com/mkopylec/charon-spring-boot-starter'
                }
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mkopylec'
                        name 'Mariusz Kopylec'
                        email 'mariusz.kopylec@o2.pl'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.mkopylec'
            artifactId 'charon-spring-boot-starter'
            version '2.5.0.2'

            from components.java
            artifact sourceJar
        }
    }
}

jacoco {
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
