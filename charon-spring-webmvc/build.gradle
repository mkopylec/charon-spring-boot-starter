apply plugin: 'jacoco'

dependencies {
    api project(':charon-common').sourceSets.main.output
    api group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    api group: 'com.squareup.okhttp3', name: 'okhttp', version: okHttpVersion

    testImplementation project(':charon-test')
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    sourceSets project(':charon-common').sourceSets.main
    reports {
        xml.enabled true
        html.enabled false
    }
}

jar {
    from project(':charon-common').sourceSets.main.output
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    from project(':charon-common').sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        charonSpringWebmvc(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom pomContent
        }
    }
}

signing {
    if (project.ext.has('signArtifacts')) {
        sign publishing.publications.charonSpringWebmvc
    }
}
